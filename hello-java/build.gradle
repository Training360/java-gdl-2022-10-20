plugins {
  id "java"
  id 'jacoco'
  id 'com.adarshr.test-logger' version '3.2.0'
  id 'com.github.johnrengelman.shadow' version '7.1.2'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

jar {
    manifest {
        attributes "Main-Class": "hello.HelloMain"
    }
}

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform {
        includeTags 'fast'
        excludeTags 'slow'
    }
    finalizedBy jacocoTestReport
}

dependencies {
    def zxingVersion = "3.5.0"
    implementation "com.google.zxing:core:${zxingVersion}"
    implementation "com.google.zxing:javase:${zxingVersion}"

    def lombokVersion = "1.18.24"
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    def junitVersion = "5.9.1"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}

jacoco {
  toolVersion = "0.8.7"
}

jacocoTestReport {
  dependsOn test
}